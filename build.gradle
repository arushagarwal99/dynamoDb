plugins {
    id 'java'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    dynamoDbLocal

    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    integrationTestCompileOnly.extendsFrom testCompileOnly
    integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

dependencies {
    implementation platform('software.amazon.awssdk:bom:2.20.56')

    implementation 'com.google.dagger:dagger:2.48'
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'org.slf4j:slf4j-simple:2.0.5'
    implementation 'software.amazon.awssdk:dynamodb'
    implementation 'software.amazon.awssdk:dynamodb-enhanced'

    compileOnly 'org.immutables:value:2.9.3'

    annotationProcessor 'com.google.dagger:dagger-compiler:2.48'
    annotationProcessor 'org.immutables:value:2.9.3'

    testImplementation 'com.amazonaws:DynamoDBLocal:2.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'

    testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.48'

    dynamoDbLocal fileTree (dir: 'lib', include: ["*.dylib", "*.so", "*.dll"])
    dynamoDbLocal 'com.amazonaws:DynamoDBLocal:2.0.0'
}

test {
    useJUnitPlatform()
}

task copyNativeDeps(type: Copy) {
    from configurations.dynamoDbLocal
    into 'build/libs'
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    dependsOn copyNativeDeps
    doFirst {
        systemProperty "java.library.path", 'build/libs'
    }

    useJUnitPlatform()
}
